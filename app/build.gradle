apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'androidx.navigation.safeargs'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    def getVersionName = { ->
        def version = new ByteArrayOutputStream()

        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = version
        }
        version = version.toString().split('-')

        return "${version[0]}.${version[1]}"
    }
    def getVersionCode = { ->
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = code
        }
        return Integer.valueOf(code.toString().trim())
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = applicationId + "-${variant.versionName}.apk"
        }
    }
    defaultConfig {
        applicationId "com.bsuirlabs.softwaredesign"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
            versionNameSuffix "-dev"
            signingConfig signingConfigs.config
        }
        prod {
            dimension "version"
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    def nav_version = "1.0.0-alpha08"

    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'com.google.firebase:firebase-auth:16.1.0'
    implementation 'com.google.firebase:firebase-storage:16.0.5'
    implementation 'com.google.firebase:firebase-database:16.0.5'
    implementation 'com.github.bumptech.glide:annotations:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'
    implementation "com.github.bumptech.glide:okhttp3-integration:4.6.1"
    implementation 'com.github.bumptech.glide:annotations:4.8.0'
    implementation 'com.firebaseui:firebase-ui-storage:4.2.1'
    implementation 'com.prof.rssparser:rssparser:1.4.4'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.code.gson:gson:2.8.5'
}
